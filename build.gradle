plugins {
    id "org.jetbrains.kotlin.jvm" version "1.4.20" apply false
    id "org.springframework.boot" version "2.4.0" apply false
    id "org.jetbrains.kotlin.plugin.spring" version "1.3.41" apply false
    id "io.spring.dependency-management" version "1.0.8.RELEASE" apply false
}

configure(subprojects) { project ->
    apply plugin: "idea"
    apply plugin: "java"
    apply plugin: "io.spring.dependency-management"
    apply plugin: "org.jetbrains.kotlin.plugin.spring"
    apply plugin: "org.jetbrains.kotlin.jvm"

    repositories {
        mavenCentral()
        jcenter()
    }

    configurations {
        ktlint
    }

    dependencyManagement {
        imports {
            mavenBom("org.springframework.boot:spring-boot-dependencies:2.4.0")
        }
    }

    dependencies {
        ktlint "com.pinterest:ktlint:0.40.0"
    }

    task ktlint(type: JavaExec, group: "verification") {
        description = "Check Kotlin code style."
        main = "com.pinterest.ktlint.Main"
        classpath = configurations.ktlint
        args "src/**/*.kt"
        // to generate report in checkstyle format prepend following args:
        // "--reporter=plain", "--reporter=checkstyle,output=${buildDir}/ktlint.xml"
        // see https://github.com/pinterest/ktlint#usage for more
    }
    check.dependsOn ktlint

    task ktlintFormat(type: JavaExec, group: "formatting") {
        description = "Fix Kotlin code style deviations."
        main = "com.pinterest.ktlint.Main"
        classpath = configurations.ktlint
        args "-F", "src/**/*.kt"
    }

}
